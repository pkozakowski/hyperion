sweep: _NL? _nls_list{prelude_statement}? statement*

?statement: binding _NL
          | all _NL
          | _block{"product",statement}    -> product
          | _block{"union",statement}      -> union
          | _block{table_header,table_row} -> table

all: identifier ":" _LBRACKET cs_list{expr} _RBRACKET

_block{header,line}: header ":" _NL _INDENT line+ _DEDENT

table_header: "table" _columns
_columns: _cs_list{identifier}
        | _LPAREN _cs_list{identifier} _RPAREN

table_row: _cs_list{expr} _NL

%import config (prelude_statement, binding, _nls_list)
%import config (identifier, expr, _cs_list, cs_list, _NL, COMMENT)
%import config (_LPAREN, _RPAREN, _LBRACKET, _RBRACKET, _LBRACE, _RBRACE)

%import common.WS_INLINE
%import config.COMMENT

%ignore WS_INLINE
%ignore COMMENT

%declare _INDENT _DEDENT
